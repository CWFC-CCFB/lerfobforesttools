buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.tmatesoft.svnkit', name: 'svnkit', version: '1.10.3'
    }
}

plugins {
    id 'idea'
    id 'eclipse'
    id 'java'
}

import org.tmatesoft.svn.core.wc.*

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(8))
  }
}

repositories {
    mavenCentral()
    mavenLocal()
}

println "Using java version " + JavaVersion.current()

sourceSets {
    main {    	
        java {
            srcDirs 'src'            
        }
        
        resources {
			srcDirs= ['src']
			exclude '**/*.java'
		}
		
		output.resourcesDir = "$buildDir/classes/java/$name"
    }

    test {
        java {
            srcDirs 'test'
        }
                
        resources {
			srcDirs= ['test']
			exclude '**/*.java'
		}
		
		output.resourcesDir = "$buildDir/classes/java/$name"
    }               
}

dependencies {
    implementation 'org.jfree:jcommon:1.0.23'
    implementation 'org.jfree:jfreechart:1.0.17'
    implementation 'jfree:jfreechart-swt:1.0.17'
    implementation 'net.sf.py4j:py4j:0.10.9.2'    
    implementation group: 'org', name: 'repicea', version: '[1.9.4,)'    
    implementation group: 'org', name: 'repicea-mathstats', version: '[1.1,)'    
    implementation group: 'org', name: 'repicea-simulation', version: '[1.0.2,)'    
    
    implementation group: 'org.apache.xmlgraphics', name: 'batik-svggen', version: '1.14'
	implementation group: 'org.apache.xmlgraphics', name: 'batik-dom', version: '1.14'
	implementation group: 'org.apache.xmlgraphics', name: 'batik-codec', version: '1.14'
    
    testImplementation "junit:junit:4.12"    
}

def getSemVersion = { ->

	def versionStdOut = new ByteArrayOutputStream()

	def cmd = ['gitversion', '/showvariable', 'SemVer']
	
	    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
	        def oscmd = ['cmd', '/c'] + cmd
	        exec {
	        	commandLine oscmd
	        	standardOutput = versionStdOut
        	}
	    
	    } else {
	        def oscmd = ['sh'] + cmd
	        exec {
	        	commandLine oscmd
	    		standardOutput = versionStdOut
    		}
    	}
    
    return versionStdOut.toString().trim()        	
}


task updateRevision(type: WriteProperties) {
//	project.ext.build = getSvnRevision()
//	outputFile = file("$buildDir/classes/java/main/lerfob/app/revision")	
//	property "Revision", project.ext.build
//	property "Version", lerfobForestToolsVersion
}

compileJava {
	project.version=getSemVersion()
	println "Building " + rootProject.name + " v" + project.version
}

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // set heap size for the test JVM(s)
    maxHeapSize = "2048m"
    
    exclude '**/ProductionEnvironmentTest.class'
}

task integrationTest(type: Test) {
	description = 'Runs the integration tests.'
    group = 'verification'
	dependsOn jar
	
	include '**/*.class'
	
	testLogging.showStandardStreams = true
    testClassesDirs = sourceSets.test.output.classesDirs
        
    // set classpath to test classes + test dependencies + JAR
    classpath = project.sourceSets.test.output + configurations.testRuntimeClasspath + files(jar.archiveFile) 
    outputs.upToDateWhen { false }
    mustRunAfter test
    maxHeapSize = "2048m"
}

check.dependsOn integrationTest

jar {
	dependsOn updateRevision
	manifest {
        attributes("Built-By": "Mathieu Fortin",
        		   "Specification-Title": "Growth Models for Northern France",
        		   "Specification-Version": archiveVersion,                   
                   "Specification-Vendor": "LERFOB and Canadian Wood Fibre Centre",
                   "Implementation-Title": "lerfobforesttools",
                   "Implementation-Vendor": "LERFOB and Canadian Wood Fibre Centre",
                   )
    }
    
	into ('/') {
		from files(['./README.txt', './COPYING.LESSER.txt'])			
	}
	
	// include all java files from main sourceset into the jar file (resources are already included)
	from sourceSets.main.allJava		
}

task fatJar(type: Jar) {
	group = 'build'
	description = 'Creates a fat jar for distribution'
	dependsOn updateRevision
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE		// to avoid duplicate files
    manifest {
         attributes("Built-By": "Mathieu Fortin",
        		   "Specification-Title": "Growth Models for Northern France",
        		   "Specification-Version": archiveVersion,                   
                   "Specification-Vendor": "LERFOB and Canadian Wood Fibre Centre",
                   "Implementation-Title": "lerfobforesttools",
                   "Implementation-Vendor": "LERFOB and Canadian Wood Fibre Centre",
                   "Main-Class": "lerfob.app.LerfobForesttools")
    }
    archiveBaseName = 'CAT'
	destinationDirectory =  file("${installDirCAT}")  
    from { sourceSets.main.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }		
}

task deleteInstallFolder(type: Delete) {
  delete "${installDir}"  
}

task install(type: Copy) {
	description = 'Copies the output jar to installation folder.'
	group = 'build'
	dependsOn jar,deleteInstallFolder,integrationTest
	from configurations.default
	from jar
	into "${installDir}"
}