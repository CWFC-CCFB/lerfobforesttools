/*
 *  Build file for gradle
 */


plugins {
    id 'com.palantir.git-version' version '3.0.0'
    id 'maven-publish'
    id 'signing'
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'


task sourcesJar(type: Jar) {
	 archiveClassifier = 'sources'
	 manifest {
        attributes("Built-By": "Mathieu Fortin",
        		   "Specification-Title": "LERFoB Foresttools sources",
	       		   "Specification-Version": project.version,                   
                   "Specification-Vendor": "CWFC-CCFB",
                   "Implementation-Title": "lerfobforesttools",
                   "Implementation-Version": project.version,
                   "Implementation-Vendor": "CWFC-CCFB",
                   )
    }
   	duplicatesStrategy = DuplicatesStrategy.EXCLUDE		// to avoid duplicate files
    
	into ('/') {
		from files(['./README.md', './LICENSE.md'])			
	}
	
	// include all java files from main sourceset into the jar file (resources are already included)
	from sourceSets.main.java
	from sourceSets.main.resources		
}

java {
  toolchain { 
        languageVersion = JavaLanguageVersion.of(8)
  }
  withSourcesJar()
  withJavadocJar()
}

repositories {
	maven {		// sonatype repository
		url "https://repo1.maven.org/maven2/"
	}
    mavenCentral()
}

println "Using java version " + JavaVersion.current()

sourceSets {
    main {    	
        java {
            srcDirs 'src'            
        }
        
        resources {
			srcDirs= ['src']
			exclude '**/*.java'
		}
		
		output.resourcesDir = "$buildDir/classes/java/$name"
    }

    test {
        java {
            srcDirs 'test'
        }
                
        resources {
			srcDirs= ['test']
			exclude '**/*.java'
		}
		
		output.resourcesDir = "$buildDir/classes/java/$name"
    }               
}

configurations {
	lerfobForesttoolsImplementation {
		extendsFrom implementation
		description = 'Main implementation'
		canBeResolved = true
	}
}


dependencies {
    implementation group: 'io.github.cwfc-ccfb', name: 'repicea', version: '[1.9.8,)'    
    implementation group: 'io.github.cwfc-ccfb', name: 'repicea-mathstats', version: '[1.3.1,)'    
    implementation group: 'io.github.cwfc-ccfb', name: 'repicea-simulation', version: '[1.0.9,)'    
    
    testImplementation "junit:junit:4.12"    
}

compileJava {
	project.version= gitVersion() 
	println "Building " + rootProject.name + " " + project.version
	println "Installation directory " + "../externallibraries/" + rootProject.name
}

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // set heap size for the test JVM(s)
    maxHeapSize = "2048m"
    
    exclude '**/ProductionEnvironmentTest.class'
}

jar {
	manifest {
         attributes("Built-By": "Mathieu Fortin",
        		   "Specification-Title": "LERFoB Foresttools",
	       		   "Specification-Version": project.version,                   
                   "Specification-Vendor": "CWFC-CCFB",
                   "Implementation-Title": "lerfobforesttools",
                   "Implementation-Version": project.version,
                   "Implementation-Vendor": "CWFC-CCFB",
                   )
    }
    
	into ('/') {
		from files(['./README.md', './LICENSE.md'])			
	}
	
	// include all java files from main sourceset into the jar file (resources are already included)
	from sourceSets.main.allJava		
}

task integrationTest(type: Test) {
	description = 'Runs the integration tests.'
    group = 'verification'
	dependsOn jar
	
	include '**/*.class'
	
	testLogging.showStandardStreams = true
    testClassesDirs = sourceSets.test.output.classesDirs
        
    // set classpath to test classes + test dependencies + JAR
    classpath = project.sourceSets.test.output + configurations.testRuntimeClasspath + files(jar.archiveFile) 
    outputs.upToDateWhen { false }
    mustRunAfter test
    maxHeapSize = "2048m"
}

check.dependsOn integrationTest

task deleteInstallFolder(type: Delete) {
  delete "../externallibraries/" + rootProject.name
}

task install(type: Copy) {
	description = 'Copies the output jar to installation folder.'
	group = 'build'
	dependsOn deleteInstallFolder,integrationTest
	from configurations.lerfobForesttoolsImplementation
	from jar
	into "../externallibraries/" + rootProject.name
}

publishing {
    publications {
        lerfobforesttools(MavenPublication) {
          	from components.java
        	pom {
        		name = 'LERFoB Foresttools'
				group = 'io.github.cwfc-ccfb'
        		description = 'Support for growth models in France'
        		url = 'https://github.com/CWFC-CCFB/lerfobforesttools'
        		properties = [
        			packaging: "jar"
	       		]
        		licenses {
        			license {
        				name = 'GNU Lesser General Public License 2.1'
        				url  = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.html'
        			}
        		}
        		developers {
        			developer {
        				id = 'matfortin'
        				name = 'Mathieu Fortin'
        				email = 'mathieu.fortin.re@gmail.com'
        			}        
        		}
        		
                scm {
          			connection = 'scm:git:https://github.com/CWFC-CCFB/lerfobforesttools'
          			developerConnection = 'scm:git:https://github.com/CWFC-CCFB/lerfobforesttools'
          			url = 'https://github.com/CWFC-CCFB/lerfobforesttools'
        		}
		     }	
        }
    }
    repositories {
        maven {
            name = 'LocalFile'
            url = "file://${buildDir}/repo"
        }

		maven {
		 	name = 'Sonatype'
		 	credentials(PasswordCredentials)
		 	url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
		}
    }
}

signing {
    sign publishing.publications.lerfobforesttools
}

publishLerfobforesttoolsPublicationToLocalFileRepository {
	dependsOn jar
}

 